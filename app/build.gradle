apply plugin: 'com.android.application'

android {
    signingConfigs {
        singnedconfig {
            storeFile file(PLATFORM_STORE_FILE)
            keyAlias PLATFORM_KEY_ALIAS
            keyPassword PLATFORM_KEY_PASSWORD
            storePassword PLATFORM_STORE_PASSWORD
        }
    }

    compileSdkVersion rootProject.ext.sdk.compileSdkVersion
    defaultConfig {
        applicationId "com.askey.dvr.cdr7010.dashcam"
        archivesBaseName = "DashCam"
        minSdkVersion rootProject.ext.sdk.minSdkVersion
        targetSdkVersion rootProject.ext.sdk.targetSdkVersion
        versionCode rootProject.ext.version.versionCode
        versionName rootProject.ext.version.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.singnedconfig
        buildConfigField "java.util.Date", "buildTime", "new java.util.Date(" + System.currentTimeMillis() + "L)"

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_ARM_NEON=TRUE", "-DANDROID_TOOLCHAIN=gcc"
                cppFlags "-fexceptions"
            }
        }

        ndk {
            abiFilters "armeabi-v7a"
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ eventBusIndex : 'com.askey.dvr.cdr7010.dashcam.EventBusIndex' ]
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            signingConfig signingConfigs.singnedconfig
            jniDebuggable true
        }
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
            signingConfig signingConfigs.singnedconfig
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    applicationVariants.all { variant ->
        variant.getPackageApplication().outputScope.apkDatas.forEach { apkData ->
            apkData.outputFileName = project.archivesBaseName + ".apk"
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation rootProject.ext.dependencies.appcompatV7
    implementation rootProject.ext.dependencies.supportV4
    implementation rootProject.ext.dependencies.supportV13
    implementation rootProject.ext.dependencies.eventbus
    implementation files('libs/com.askey.platform.jar')
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.1.1'
    annotationProcessor "com.google.auto.value:auto-value:1.5.2"
    compileOnly "com.google.auto.value:auto-value:1.5.2"

    testImplementation rootProject.ext.dependencies.junit
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'de.mindpipe.android:android-logging-log4j:1.0.3'
    implementation 'log4j:log4j:1.2.17'
    implementation files('src/main/libs/adas.jar')
}


